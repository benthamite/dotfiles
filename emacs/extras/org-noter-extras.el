;;; org-noter-extras.el --- Extensions for org-noter -*- lexical-binding: t -*-

;; Copyright (C) 2023

;; Author: Pablo Stafforini
;; URL: https://github.com/benthamite/dotfiles/tree/master/emacs/extras/org-noter-extras.el
;; Version: 0.1

;; This file is NOT part of GNU Emacs.

;; This program is free software; you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation, either version 3 of the License, or
;; (at your option) any later version.
;;
;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.
;;
;; You should have received a copy of the GNU General Public License
;; along with this program.  If not, see <https://www.gnu.org/licenses/>.

;;; Commentary:

;; Extensions for `org-noter'.

;;; Code:

(require 'org-noter)

;;;; Variables

(defgroup org-noter-extras ()
  "Extensions for `org-noter'."
  :group 'org-noter-extras)

(defvar org-noter-extras-dehyphenate-hyphen "-"
  "Hyphen to be removed by `org-noter-extras-dehyphenate'.")

(defconst org-noter-highlight-heading-regexp "Highlight on page \\(.*\\)"
  "Regexp matching the highlight heading generated by `org-noter'.")

;;;; Functions

(defun org-noter-extras-cleanup-annotation (title)
  "Cleanup the annotation at point.
Replaces the generic \"Highlight on page X\" generated by
`org-noter-create-skeleton' with a custom TITLE and encloses the content of the
heading in a quote."
  (interactive "sTitle: ")
  (require 'unfill)
  (save-restriction
    (save-excursion
      (let ((content (org-noter-get-annotation-contents)))
	(widen)
	(org-cut-subtree)
	(org-previous-visible-heading 1)
	(let* ((highlight-heading (org-get-heading t t t t))
	       (page (progn
		       (string-match org-noter-highlight-heading-regexp highlight-heading)
		       (match-string 1 highlight-heading))))
	  (org-edit-headline (format "%s, p. %s" title page)))
	(org-narrow-to-subtree)
	(goto-char (point-max))
	(insert (concat "\n#+begin_quote\n" content "#+end_quote\n")))
      (org-next-visible-heading 1)
      (org-fold-show-subtree))))

(defun org-noter-get-annotation-contents ()
  "Return cleaned-up annotation contents in subtree."
  (let ((initial-heading (org-get-heading t t t t)))
    (cond
     ((string-match org-noter-highlight-heading-regexp initial-heading)
      (org-back-to-heading)
      (org-next-visible-heading 1))
     ((string-match "Contents" initial-heading)
      (org-back-to-heading))
     (t
      (user-error "You must be either in a \"contents\" heading or in a \"highlight on page\" Heading")))
    (org-narrow-to-subtree)
    (org-end-of-meta-data t)
    (narrow-to-region (point) (point-max))
    (unfill-region (point) (point-max))
    (org-noter-extras-dehyphenate)
    (buffer-substring-no-properties (point) (point-max))))

(defun org-noter-append-to-previous-annotation ()
  "Append the annotation at point to the previous annotation."
  (interactive)
  (let ((content (org-noter-get-annotation-contents)))
    (widen)
    (org-cut-subtree)
    (re-search-backward "#\\+end_quote")
    (left-char)
    (insert (concat " " content))))

;; TODO: find `org-noter' hook to run this automatically
(defun org-noter-extras-dehyphenate ()
  "Remove leftover hyphens in hyphenated text.
Operate on the current paragraph, or the region if active."
  (interactive)
  (let ((start (if (use-region-p)
		   (region-beginning)
		 (save-excursion (backward-paragraph) (point))))
	(end (if (use-region-p)
		 (region-end)
	       (save-excursion (forward-paragraph) (point))))
	(regexp (format "\\([[:alpha:]]\\)%s \\([[:alpha:]]\\)"
			org-noter-extras-dehyphenate-hyphen)))
    (save-excursion
      (goto-char start)
      (while (re-search-forward regexp end t)
	(replace-match "\\1\\2")))))

;; TODO: find `org-noter' hook to run this automatically
(defun org-noter-extras-highlight-offset (offset)
  "Fix numbering mismatch between PDF and book/article pages.
OFFSET is the page number of the PDF (displayed in the document viewer) minus
the page number in the book or article (shown on the printed page). Thus, if the
PDF’s page number is 220 and the book’s page number is 210 , OFFSET is 10. The
number will typically (but need not) be positive, since the PDF page number
tends to be higher than the book page number."
  (interactive "nOffset (PDF – book): ")
  (save-excursion
    (goto-char (point-min))
    (while (re-search-forward "\\(Highlight on page \\)\\(.*$\\)" nil t)
      (let* ((num (number-to-string (- (string-to-number (match-string 2)) offset)))
	     (replacement (concat (match-string 1) num)))
	(replace-match replacement)))))

(defun org-noter-extras-set-hyphen ()
  "Set hyphen character for de-hyphenation."
  (interactive)
  (let ((hyphen (completing-read (format "Hyphen character (currently `%s'): "
					 org-noter-extras-dehyphenate-hyphen)
				 '("-" "­"))))
    (setq org-noter-extras-dehyphenate-hyphen hyphen)))

(provide 'org-noter-extras)
;;; org-noter-extras.el ends here
